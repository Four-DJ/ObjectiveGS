//command: nmap
if params.len != 1 || params[0] == "-h" || params[0] == "--help" {
    exit(command_info("nmap_usage"))
}
if !is_valid_ip(params[0]) { 
    exit("nmap: invalid ip address")
}
if !get_shell().host_computer().is_network_active() {
    exit("nmap: No internet access.")
}

ipAddress = params[0]
if ipAddress == "127.0.0.1" {
    ipAddress = get_shell().host_computer().local_ip()
}

isLanIp = is_lan_ip(ipAddress)
router = isLanIp ? get_router() : get_router(ipAddress)

if router == null {
    exit("nmap: ip address not found")
}
ports = isLanIp ? router.device_ports(ipAddress) : router.used_ports()

if ports == null {
    exit("nmap: ip address not found")
}
if typeof(ports) == "string" { 
    exit(ports)
}
      
info = "PORT STATE SERVICE VERSION LAN"   
print("\nStarting nmap v1.1 at ${current_date}")
print("Interesting ports on ${params[0]}\n")
if ports.len == 0 { 
    exit("Scan finished. No open ports.")
}

for port in ports {
   service_info = router.port_info(port)
   lan_ips = port.get_lan_ip
   port_status = port.is_closed ? "closed" : "open"
   info += "\n${port.port_number} ${port_status} ${service_info} ${lan_ips}"
}
print("${format_columns(info)}\n")